# Calculate resource names
locals {
  name_replacements = {
    workload       = var.resource_name_workload
    environment    = var.resource_name_environment
    location       = var.location
    location_short = var.resource_name_location_short == "" ? module.regions.regions_by_name[var.location].geo_code : var.resource_name_location_short
    uniqueness     = random_string.unique_name.id
    sequence       = format("%03d", var.resource_name_sequence_start)
  }

  resource_names = { for key, value in var.resource_name_templates : key => templatestring(value, local.name_replacements) }
}

locals {
  default_audience_name       = "api://AzureADTokenExchange"
  github_issuer_url           = "https://token.actions.githubusercontent.com"
  create_agent_infrastructure = var.use_self_hosted_agents && var.existing_runner_group_name == null
  create_vnet_infrastructure  = local.create_agent_infrastructure && var.existing_virtual_network_resource_id == null
  is_self_hosted              = var.use_self_hosted_agents || var.existing_runner_group_name != null
  effective_vnet_resource_id  = var.existing_virtual_network_resource_id != null ? var.existing_virtual_network_resource_id : (local.create_vnet_infrastructure ? module.virtual_network[0].resource_id : null)
  effective_agents_subnet_id  = var.existing_agents_subnet_resource_id != null ? var.existing_agents_subnet_resource_id : (local.create_vnet_infrastructure ? module.virtual_network[0].subnets["agents"].resource_id : null)
  effective_pe_subnet_id      = var.existing_private_endpoints_subnet_resource_id != null ? var.existing_private_endpoints_subnet_resource_id : (local.create_vnet_infrastructure ? module.virtual_network[0].subnets["private_endpoints"].resource_id : null)
  use_private_networking      = local.effective_vnet_resource_id != null
  create_template_repository  = var.existing_template_repository_name == null
  effective_template_repo_name = var.existing_template_repository_name != null ? var.existing_template_repository_name : github_repository.template[0].name
  effective_ci_template_path  = coalesce(var.ci_template_path, ".github/workflows/ci-template.yaml")
  effective_cd_template_path  = coalesce(var.cd_template_path, ".github/workflows/cd-template.yaml")
  create_approval_team        = var.existing_approvers_team_id == null && length(var.approvers) > 0
  effective_approvers_team_id = var.existing_approvers_team_id != null ? var.existing_approvers_team_id : (local.create_approval_team ? github_team.this[0].id : null)
  has_approvers               = var.existing_approvers_team_id != null || length(var.approvers) > 0
}

locals {
  environments = { for key, value in var.environments : key => {
    display_order         = value.display_order
    display_name          = value.display_name
    has_approval          = value.has_approval
    dependent_environment = value.dependent_environment
    scope                 = value.scope
    subscription_id       = coalesce(value.subscription_id, data.azapi_client_config.current.subscription_id)
    resource_id           = value.resource_id
    create_resource_group = value.scope == "resource_group" && value.resource_id == null && value.resource_group_create
    plan_role_definition_id_or_name  = value.plan_role_definition_id_or_name
    apply_role_definition_id_or_name = value.apply_role_definition_id_or_name
    resource_group_name = templatestring(value.resource_group_name_template, {
      workload    = local.name_replacements.workload
      environment = key
      location    = local.name_replacements.location
      sequence    = local.name_replacements.sequence
    })
    user_assigned_managed_identity_name_template = value.user_assigned_managed_identity_name_template
  } }

  environment_split_type = {
    plan  = "plan"
    apply = "apply"
  }

  environment_split = { for environment_split in flatten([for env_key, env_value in local.environments : [
    for split_key, split_value in local.environment_split_type : {
      composite_key      = "${env_key}-${split_key}"
      environment        = env_key
      type               = split_key
      required_templates = split_key == local.environment_split_type.plan ? ["ci-template.yaml", "cd-template.yaml"] : ["cd-template.yaml"]
      has_approval       = env_value.has_approval
      user_assigned_managed_identity_name = templatestring(env_value.user_assigned_managed_identity_name_template, {
        workload    = local.name_replacements.workload
        environment = env_key
        type        = split_key
        location    = local.name_replacements.location
        sequence    = local.name_replacements.sequence
      })
    }
  ]]) : environment_split.composite_key => environment_split }
}
